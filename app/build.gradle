apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.pfariasmunoz.paintapp"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled true
            // Uses new built-in shrinker http://tools.android.com/tech-docs/new-build-system/built-in-shrinker
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }

        release {
            minifyEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }

    flavorDimensions "default"

    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
            dimension "default"
        }
        prod {
            dimension "default"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.flavors[0].name == 'mock') {
            variant.ignore = true
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

}

kotlin {
    experimental {
        coroutines "enable"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // App's dependencies, including test
    implementation "com.android.support:appcompat-v7:$versions.support"
    implementation "com.android.support:design:$versions.support"
    implementation "com.android.support:recyclerview-v7:$versions.support"
    implementation "com.android.support:support-v4:$versions.support"
    implementation "com.android.support.test.espresso:espresso-idling-resource:$versions.espresso"
    implementation "android.arch.persistence.room:runtime:$versions.arch"
    implementation "com.android.support.constraint:constraint-layout:$versions.constraint"

    // Kotlin
    kapt "android.arch.persistence.room:compiler:$versions.arch"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$versions.kotlin"

    // Dependencies for local unit tests
    testImplementation "junit:junit:$versions.junit"
    testImplementation "org.hamcrest:hamcrest-all:$versions.hamcrest"
    testImplementation "org.mockito:mockito-core:$versions.mockito"

    // Android Testing Support Library's runner and rules
    androidTestImplementation "com.android.support.test:runner:$versions.runner"
    androidTestImplementation "com.android.support.test:rules:$versions.runner"

    // Dependencies for Android unit tests
    androidTestImplementation "junit:junit:$versions.junit"
    androidTestImplementation "org.mockito:mockito-android:$versions.mockito"

    // Espresso UI Testing
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$versions.espresso"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$versions.espresso"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$versions.espresso"

    // Resolve conflicts between main and test APK:
    androidTestImplementation "com.android.support:support-annotations:$versions.support"
    androidTestImplementation "com.android.support:support-v4:$versions.support"
    androidTestImplementation "com.android.support:recyclerview-v7:$versions.support"
    androidTestImplementation "com.android.support:appcompat-v7:$versions.support"
    androidTestImplementation "com.android.support:design:$versions.support"
    androidTestImplementation "com.android.support.test.espresso.idling:idling-concurrent:$versions.espresso"
    androidTestImplementation "android.arch.persistence.room:testing:$versions.arch"

    // Dagger 2
    implementation "com.google.dagger:dagger:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    // Other
    implementation "com.google.code.findbugs:jsr305:2.0.1"
}
